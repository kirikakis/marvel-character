group 'com.github.kirikakis.marvel.character'

apply plugin:'java'
apply plugin:'base'
apply plugin:'application'
apply plugin:'groovy'
apply plugin:'idea'

def versions = [
        jdk:            '1.8',
        springBoot:     '1.5.8.RELEASE',
        swagger:        '2.7.0',
        h2:             '1.4.196',
        jsoup:          '1.10.2',
        gcTranslate:    '1.10.0',
        groovy:         '2.4.10',
        spock:          '1.1-groovy-2.4'

]

sourceCompatibility = versions.jdk

mainClassName = 'com.github.kirikakis.marvel.character.Main'
applicationName = 'marvel-character'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web:' + versions.springBoot
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:' + versions.springBoot
    compile 'org.springframework.boot:spring-boot-starter-data-rest:' + versions.springBoot
    compile 'io.springfox:springfox-swagger2:' + versions.swagger
    compile 'io.springfox:springfox-swagger-ui:' + versions.swagger
    compile 'com.h2database:h2:' + versions.h2
    compile 'org.jsoup:jsoup:' + versions.jsoup
    compile 'com.google.cloud:google-cloud-translate:' + versions.gcTranslate

    testCompile 'org.codehaus.groovy:groovy-all:' + versions.groovy
    testCompile 'org.spockframework:spock-core:' + versions.spock
    testCompile 'org.springframework.boot:spring-boot-starter-test:' + versions.springBoot
}

tasks.withType(JavaExec) {
    // Assign all Java system properties from
    // the command line to the JavaExec task.
    systemProperties System.properties
}

startScripts {
    // Add config, libs folders to classpath. Using workaround at
    // https://discuss.gradle.org/t/classpath-in-application-plugin-is-building-always-relative-to-app-hoe-lib-directory/2012
    classpath += files('config')
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\config', '%APP_HOME%\\config')
        unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/config', '$APP_HOME/config')
    }
}

distZip {
    into(project.name + '/config') {
        from 'src/main/resources'
    }
}

distTar {
    into(project.name + '/config') {
        from 'src/main/resources'
    }
}
